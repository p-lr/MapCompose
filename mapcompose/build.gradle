import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "org.jetbrains.kotlin.plugin.compose" version "$kotlin_version"
    id "com.vanniktech.maven.publish" version "0.33.0"
}

android {
    compileSdk = 36

    defaultConfig {
        minSdk = 21
        targetSdk = 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }

    namespace = 'ovh.plrapps.mapcompose'
}

dependencies {
    // Compose - See https://developer.android.com/jetpack/compose/setup#bom-version-mapping
    api platform('androidx.compose:compose-bom:2025.08.01')
    api "androidx.compose.foundation:foundation"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-util"
    implementation "androidx.compose.ui:ui-unit"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"
    testImplementation 'org.robolectric:robolectric:4.16'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}

ext.'signing.keyId' = System.getenv('signingKeyId')
ext.'signing.password' = System.getenv('signingPwd')
ext.'signing.secretKeyRingFile' = System.getenv('signingKeyFile')

mavenPublishing {
    configure(new AndroidSingleVariantLibrary("release", true, true))

    coordinates(GROUP, ARTIFACT_ID, VERSION_NAME)
    pom {
        name = POM_NAME
        description = POM_DESCRIPTION
        url = POM_URL
        scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
        }

        licenses {
            license {
                name = POM_LICENCE_NAME
                url = POM_LICENCE_URL
                distribution = POM_LICENCE_DIST
            }
        }

        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
                url = POM_DEVELOPER_URL
            }
        }
    }

    publishToMavenCentral(false)
    signAllPublications()
}
