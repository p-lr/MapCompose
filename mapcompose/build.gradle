plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    namespace 'ovh.plrapps.mapcompose'
}

dependencies {
    // Compose - See https://developer.android.com/jetpack/compose/setup#bom-version-mapping
    api platform('androidx.compose:compose-bom:2023.10.00')
    api "androidx.compose.foundation:foundation"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-util"
    implementation "androidx.compose.ui:ui-unit"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"
    testImplementation 'org.robolectric:robolectric:4.7.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
}

ext.'signing.keyId' = System.getenv('signingKeyId')
ext.'signing.password' = System.getenv('signingPwd')
ext.'signing.secretKeyRingFile' = System.getenv('signingKeyFile')

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = GROUP
                artifactId = ARTIFACT_ID
                version = VERSION_NAME  // see gradle.properties

                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    description = POM_DESCRIPTION
                    url = POM_URL

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            url = POM_DEVELOPER_URL
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = System.getenv('releaseRepositoryUrl')
                def snapshotsRepoUrl = System.getenv('snapshotRepositoryUrl')
                url = !VERSION_NAME.contains("SNAPSHOT") ? releasesRepoUrl : snapshotsRepoUrl

                credentials(PasswordCredentials) {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}